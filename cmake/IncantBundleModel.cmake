if(NOT DEFINED INCANT_MODEL_FILE OR NOT DEFINED INCANT_MODEL_NAME)
    message(FATAL_ERROR "INCANT_MODEL_FILE and INCANT_MODEL_NAME must be set")
endif()

if(NOT EXISTS "${INCANT_MODEL_FILE}")
    message(STATUS "Incant model file not found: ${INCANT_MODEL_FILE}")
    return()
endif()

set(bundle_path "")
if(DEFINED INCANT_PLUGIN_BUNDLE AND EXISTS "${INCANT_PLUGIN_BUNDLE}")
    set(bundle_path "${INCANT_PLUGIN_BUNDLE}")
    set(bundle_models_dir "${bundle_path}/Contents/Resources/models")
    file(MAKE_DIRECTORY "${bundle_models_dir}")
    file(COPY_FILE "${INCANT_MODEL_FILE}" "${bundle_models_dir}/${INCANT_MODEL_NAME}" ONLY_IF_DIFFERENT)
endif()

set(copy_dir "${INCANT_PLUGIN_COPY_DIR}")
if(copy_dir MATCHES "\\$<")
    set(copy_dir "")
endif()

if(copy_dir STREQUAL "" AND APPLE AND NOT bundle_path STREQUAL "")
    get_filename_component(bundle_ext "${bundle_path}" EXT)
    if(bundle_ext STREQUAL ".vst3")
        set(copy_dir "$ENV{HOME}/Library/Audio/Plug-Ins/VST3")
    elseif(bundle_ext STREQUAL ".component")
        set(copy_dir "$ENV{HOME}/Library/Audio/Plug-Ins/Components")
    elseif(bundle_ext STREQUAL ".app")
        set(copy_dir "$ENV{HOME}/Applications")
    endif()
endif()

if(NOT copy_dir STREQUAL "" AND NOT bundle_path STREQUAL "")
    get_filename_component(bundle_name "${bundle_path}" NAME)
    set(dest_bundle "${copy_dir}/${bundle_name}")
    if(EXISTS "${dest_bundle}")
        set(dest_models_dir "${dest_bundle}/Contents/Resources/models")
        file(MAKE_DIRECTORY "${dest_models_dir}")
        file(COPY_FILE "${INCANT_MODEL_FILE}" "${dest_models_dir}/${INCANT_MODEL_NAME}" ONLY_IF_DIFFERENT)
    else()
        message(STATUS "Incant plugin copy destination not found yet: ${dest_bundle}")
    endif()
endif()
